The following REXX exec can be used to check whether or not the consistency
token recorded in the DB2 catalog matches the value in the DBRM and the
program load module.  The REXX works with either plans or packages.  You
can also enter wildcard names containing % to get a list of plans and/or
packages.  This REXX exec is being provided as is.  There is now warranty
or gaurantee, either explicit or implied.  Use at your own risk.  etc. etc.



/* REXX                                                               */
/*
THIS REXX EXEC CAN BE USED BY DB2 DATABASE ADMINISTRATORS OR PROGRAMMERS
TO VERIFY THAT A PROGRAM HAS BEEN BOUND ON A PARTICULAR SUBSYSTEM AND
THE LOAD MODULE HAS BEEN COPIED TO THE APPROPRIATE LOAD LIBRARY.  THE
CONSISTENCY TOKEN THAT IS FOUND IN THE DB2 CATALOG IS COMPARED TO THE
DBRMLIB MEMBER AND TO THE LOADLIB MEMBER.  ANY MISMATCHES ARE NOTED.
THIS REXX WORKS WITH EITHER PLAN NAMES OR PACKAGE NAMES.  YOU CAN ALSO
USE THE PERCENT SIGN (%) AS A WILDCARD CHARACTER.
*/

SAY
SAY 'PICK A SUBSYSTEM (DSNM, DSNP, DSNQ, OR DSNR) ...'
PULL SSID

SAY
SAY 'ENTER THE NAME OF THE PLAN OR PACKAGE ...'
PULL PACKAGE

SAY
SAY 'ENTER THE FULLY QUALIFIED LIBRARY NAME WHERE THE PROGRAM'
SAY "IS LOCATED (DON'T ENTER ANY QUOTES) ..."
PULL LIBRARY

CALL PKGSEARCH
CALL PLNSEARCH

IF OUTPKG.0>0 THEN DO
    SAY
    SAY 'THE FOLLOWING PACKAGES WERE FOUND IN DB2 SUBSYSTEM' SSID '...'
    CALL LISTPKGS
END

IF OUTPLAN.0>0 THEN DO
    SAY
    SAY 'THE FOLLOWING PLANS WERE FOUND IN DB2 SUBSYSTEM' SSID '...'
    CALL LISTPLANS
END

IF OUTPKG.0=0 & OUTPLAN.0=0 THEN DO
    SAY
    SAY PACKAGE "ISN'T A VALID PLAN OR PACKAGE NAME FOR",
        "DB2 SUBSYSTEM" SSID"."
    SAY 'MAKE SURE THAT YOU ARE LOGGED ONTO TSO ON THE SAME'
    SAY 'SYSTEM WHERE' SSID 'IS RUNNING AND THAT YOU ARE IN ISPF.'
END
ELSE DO
    SAY
    SAY 'CONTOKEN SUCCESSFUL COMPLETION'
END

EXIT

LISTPKGS:
/**********************************************************************/
/* PROCESS ALL OF THE PACKAGES THAT MATCH THE SEARCH CRITERIA         */
/**********************************************************************/
DO I=1 TO OUTPKG.0
    COLLID=SUBSTR(OUTPKG.I,1,18)
    NAME=SUBSTR(OUTPKG.I,19,8)
    CONTOK=SUBSTR(OUTPKG.I,27,8)
    LIBLEN=SUBSTR(OUTPKG.I,35,2)
    PDSLEN=C2D(LIBLEN)
    DBRMLIB=SUBSTR(OUTPKG.I,37,PDSLEN)
    BINDTIME=SUBSTR(OUTPKG.I,81,26)
    CONTOK=C2X(CONTOK)
    SAY
    SAY 'PACKAGE:  '||LEFT(NAME,10)||'COLLID: '||LEFT(COLLID,20)||,
        'TOKEN: '||LEFT(CONTOK,16)

    CALL FINDMATCH
END

RETURN

LISTPLANS:
/**********************************************************************/
/* PROCESS ALL OF THE PLANS THAT MATCH THE SEARCH CRITERIA            */
/**********************************************************************/
DO I=1 TO OUTPLAN.0
    NAME=SUBSTR(OUTPLAN.I,1,8)
    DBRMNAME=SUBSTR(OUTPLAN.I,9,8)
    BINDTIME=SUBSTR(OUTPLAN.I,17,26)
    CONTOK=SUBSTR(OUTPLAN.I,43,8)
    DBRMLIB=STRIP(SUBSTR(OUTPLAN.I,51,44))
    CONTOK=C2X(CONTOK)
    SAY
    SAY 'PLAN: '||LEFT(NAME,10)||'DBRM: '||LEFT(DBRMNAME,10)||,
        'TOKEN: '||LEFT(CONTOK,16)

    CALL FINDMATCH
END

RETURN

FINDMATCH:
/**********************************************************************/
/* SEARCH THE DBRMLIB AND LOADLIB FOR A MATCH                         */
/**********************************************************************/
SAY 'BINDTIME: '||LEFT(BINDTIME,26)

TOK=CONTOK
PROGRAM=NAME

CALL DBRMSEARCH

IF  LMFINDRC=8 THEN DO
    SAY 'DBRMLIB:  '||LEFT(DBRMLIB,46)||'NOT FOUND'
END
IF OK THEN DO
    SAY 'DBRMLIB:  '||LEFT(DBRMLIB,46)||'MATCH'
END
IF ^OK & LMFINDRC^=8 THEN DO
    SAY 'DBRMLIB:  '||LEFT(DBRMLIB,46)||'MISMATCH'
END

CALL LIBSEARCH

IF  LMFINDRC=8 THEN DO
    SAY 'LOADLIB:  '||LEFT(LIBRARY,46)||'NOT FOUND'
END
IF OK THEN DO
    SAY 'LOADLIB:  '||LEFT(LIBRARY,46)||'MATCH'
END
IF ^OK & LMFINDRC^=8 THEN DO
    SAY 'LOADLIB:  '||LEFT(LIBRARY,46)||'MISMATCH'
END

RETURN

DBRMSEARCH:
/**********************************************************************/
/* READ DBRM LIBRARIES THROUGH LM-SERVICES                            */
/**********************************************************************/
OK=0

ADDRESS ISPEXEC "LMINIT DATAID(DD1) DATASET('"DBRMLIB"') ENQ(SHR)"
IF RC^=0 THEN DO
    SAY'LMINIT RC= 'RC' -->EXIT'
    EXIT
END

ADDRESS ISPEXEC "LMOPEN DATAID("DD1") OPTION(INPUT)"
IF RC^=0 THEN DO
    SAY'LMOPEN RC= 'RC' -->EXIT'
    EXIT
END
LMFINDRC=0

ADDRESS ISPEXEC "LMMFIND DATAID("DD1") MEMBER("PROGRAM")"
IF RC^=0 THEN DO
    LMFINDRC=RC
    ADDRESS ISPEXEC "LMCLOSE DATAID("DD1")"
    ADDRESS ISPEXEC "LMFREE DATAID("DD1")"
    RETURN
END

ADDRESS ISPEXEC "LMGET DATAID("DD1") MODE(INVAR) DATALOC(RECORD)",
    "DATALEN(LEN) MAXLEN(28000)"
IF RC=0 THEN DO
    HEX=C2X(RECORD)
    IF POS(TOK,HEX,1)>0 THEN OK=1
END

ADDRESS ISPEXEC "LMCLOSE DATAID("DD1")"
ADDRESS ISPEXEC "LMFREE DATAID("DD1")"
RETURN

LIBSEARCH:
/**********************************************************************/
/* READ LOADLIBRARIES THROUGH LM-SERVICES                             */
/**********************************************************************/
TOK1=SUBSTR(TOK,1,8)
TOK2=SUBSTR(TOK,9,8)
TOKLOAD=TOK2''TOK1     /* COBOL*/
OK=0

ADDRESS ISPEXEC "LMINIT DATAID(DD1) DATASET('"LIBRARY"') ENQ(SHR)"
IF RC^=0 THEN DO
    SAY'LMINIT RC= 'RC' -->EXIT'
    EXIT
END

ADDRESS ISPEXEC "LMOPEN DATAID("DD1") OPTION(INPUT)"
IF RC^=0 THEN DO
    SAY'LMOPEN RC= 'RC' -->EXIT'
    EXIT
END
LMFINDRC=0

ADDRESS ISPEXEC "LMMFIND DATAID("DD1") MEMBER("PROGRAM")"
IF RC^=0 THEN DO
    LMFINDRC=RC
    ADDRESS ISPEXEC "LMCLOSE DATAID("DD1")"
    ADDRESS ISPEXEC "LMFREE DATAID("DD1")"
    RETURN
END

XRC=0
DO UNTIL XRC=8
    ADDRESS ISPEXEC "LMGET DATAID("DD1") MODE(INVAR) DATALOC(RECORD)",
        "DATALEN(LEN) MAXLEN(28000)"
    IF RC^=0 THEN DO
        ADDRESS ISPEXEC "LMCLOSE DATAID("DD1")"
        ADDRESS ISPEXEC "LMFREE DATAID("DD1")"
        RETURN
    END
    XRC=RC
    HEX=C2X(RECORD)

    IF POS(TOKLOAD,HEX,1)>0 THEN OK=1
END

ADDRESS ISPEXEC "LMCLOSE DATAID("DD1")"
ADDRESS ISPEXEC "LMFREE DATAID("DD1")"
RETURN

PKGSEARCH:
/**********************************************************************/
/* SEARCH FOR PACKAGE INFORMATION IN THE DB2 CATALOG                  */
/**********************************************************************/
"ALLOC F(SYSIN) SPACE(1 1) UNIT(SYSDA) RECFM(F B) LRECL(80) REUSE"
"ALLOC F(SYSPRINT) DUMMY REUSE"
"ALLOC F(SYSPUNCH) DUMMY REUSE"
"ALLOC F(SYSREC00) SPACE(1 1) UNIT(SYSDA) RECFM(F B) LRECL(80)"

PACK="'"PACKAGE"'"
SQL.1='SELECT COLLID, NAME, CONTOKEN, PDSNAME, BINDTIME'
SQL.2='FROM SYSIBM.SYSPACKAGE'
IF POS('%',PACKAGE) ^=0 THEN DO
    SQL.3='WHERE NAME LIKE 'PACK';'
END
ELSE DO
    SQL.3='WHERE NAME = 'PACK';'
END

"EXECIO * DISKW SYSIN (STEM SQL. FINIS)"
"NEWSTACK"
Q1="RUN PROGRAM(DSNTIAUL) PARM('SQL') PLAN(DSNTIB71) LIB('DSN710."||,
    SSID".RUNLIB.LOAD')"
QUEUE Q1
QUEUE 'END'
"DSN SYSTEM("SSID")"
"DELSTACK"

"EXECIO * DISKR SYSREC00 (STEM OUTPKG. FINIS)"
"FREE F(SYSREC00)"
"FREE  F(SYSPUNCH)"
"ALLOC F(SYSIN) DA(*) SHR REUS"
"ALLOC F(SYPRINT) DA(*) SHR REUS"
 RETURN

PLNSEARCH:
/**********************************************************************/
/* SEARCH FOR PLAN INFORMATION IN THE DB2 CATALOG                     */
/**********************************************************************/
"ALLOC F(SYSIN) SPACE(1 1) UNIT(SYSDA) RECFM(F B) LRECL(80) REUSE"
"ALLOC F(SYSPRINT) DUMMY REUSE"
"ALLOC F(SYSPUNCH) DUMMY REUSE"
"ALLOC F(SYSREC00) SPACE(1 1) UNIT(SYSDA) RECFM(F B) LRECL(80)"

PLAN="'"PACKAGE"'"
SQL.1='SELECT A.NAME, B.NAME, A.BOUNDTS, B.TIMESTAMP, B.PDSNAME'
SQL.2='FROM SYSIBM.SYSPLAN A, SYSIBM.SYSDBRM B'
SQL.3='WHERE A.NAME = B.PLNAME'
IF POS('%',PACKAGE) ^=0 THEN DO
    SQL.4='AND A.NAME LIKE 'PLAN';'
END
ELSE DO
    SQL.4='AND A.NAME = 'PLAN';'
END

"EXECIO * DISKW SYSIN (STEM SQL. FINIS)"
"NEWSTACK"
Q1="RUN PROGRAM(DSNTIAUL) PARM('SQL') PLAN(DSNTIB71) LIB('DSN710."||,
    SSID".RUNLIB.LOAD')"
QUEUE Q1
QUEUE 'END'
"DSN SYSTEM("SSID")"
"DELSTACK"

"EXECIO * DISKR SYSREC00 (STEM OUTPLAN. FINIS)"
"FREE F(SYSREC00)"
"FREE  F(SYSPUNCH)"
"ALLOC F(SYSIN) DA(*) SHR REUS"
"ALLOC F(SYPRINT) DA(*) SHR REUS"
 RETURN
